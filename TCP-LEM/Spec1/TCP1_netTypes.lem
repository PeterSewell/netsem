(*
(* A HOL98 specification of TCP *)

(* Type definitions of the network and its components *)

(*[ RCSID "$Id: TCP1_netTypesScript.sml,v 1.45 2004/12/09 15:43:08 kw217 Exp $" ]*)

(* standard prefix *)
open HolKernel boolLib Parse
infix THEN THENC |-> ##

open bossLib

open HolDoc

local open TCP1_baseTypesTheory
in end

local open arithmeticTheory stringTheory pred_setTheory bagTheory
           integerTheory finite_mapTheory realTheory word32Theory word16Theory in end;

val _ = new_theory "TCP1_netTypes";

val _ = Version.registerTheory "$RCSfile: TCP1_netTypesScript.sml,v $" "$Revision: 1.45 $" "$Date: 2004/12/09 15:43:08 $";

*)

open import Pervasives_extra
open TCP1_lemSupport
open TCP1_baseTypes

(*: @chapter [[TCP1_netTypes]] Network datagram types

This file defines the types of the datagrams that appear on the
network, with an IP message being either a TCP segment, a UDP
datagram, or an ICMP datagram.

These types abstract from most fields of the IP header: version, header
length, type of service, identification, DF, MF, and fragment offset,
time to live, header checksum, and IP options.
%
They faithfully model the IP header fields: protocol (TCP, UDP,
or ICMP), total length, source address, and destination address.
%
The [[tcpSegment]] type abstracts from the TCP checksum, reserved, and
padding fields of the TCP header, from the ordering of TCP options,
and from ill-formed TCP options.
It faithfully models all other fields.
%
The [[udpDatagram]] type abstracts from the UDP checksum but
faithfully models all other fields.
%
Lengths are represented by allowing simple lists of data bytes rather
than explicit length fields.
%
All these types collapse the encapsulation of TCP/UDP/ICMP within IP,
flattening them into single records, to reduce syntactic noise
throughout the specification.

For ease of comparison we reproduce the RFC 791/793/768 header formats below.

\begin{alltt}
\input{inetheaders.inc}
\end{alltt}


:*)


(* -------------------------------------------------- *)
(*                        TCP Level 2,3               *)
(* -------------------------------------------------- *)

(*: @section [[netty_tcp]] TCP TCP segments

TCP segments (really \emph{datagrams}, since we include the IP data)
are modelled as follows.

:*)

(* this is the spec's basic view of what appears on the wire.  In some
   places (host outqueues, network rules) we want to treat TCP
   segments and ICMP messages uniformly.  We do this by record overlay
   magic, making it appear as if all share some record accessors. *)

(* this is taken from slurp.ml 1.11, with type changes that should perhaps be reverse propagated *)

(* arith types need sorting out *)

(* really, of course, this is a type of TCP _datagrams_...*)


type 
(*: TCP datagram type :*)
tcpSegment
  = <| is1 : maybe ip ;      (* source IP *)
       is2 : maybe ip ;      (* destination IP *)
       ps1 : maybe port;     (* source port *)
       ps2 : maybe port;     (* destination port *)
       seq : tcp_seq_local;   (* sequence number       *)
       ack : tcp_seq_foreign; (* acknowledgment number *)
       URG : bool;
       ACK : bool;
       PSH : bool;
       RST : bool;
       SYN : bool;
       FIN : bool;
       win : word16;        (* window size (unsigned) *)
       ws  : maybe byte;   (* TCP option: window scaling; typically 0..14 *)
       urp : word16;        (* urgent pointer (unsigned) *)
       mss : maybe word16;             (* TCP option: maximum segment size (unsigned) *)
       ts  : maybe (ts_seq * ts_seq);  (* TCP option: RFC1323 timestamp value and echo-reply *)
       data : list byte
    |>

(*: @description
The use of "local" and "foreign" here is with respect to the \emph{sending} TCP.

:*)



(* we say this in the TR, so not repeating it here (?)

consider handling TCP options differently: store just the
          raw bytes here, and interpret them with a function.  That
          function would be like |tcp_dooptions|, reading the options
          into a record of options like the tail of the above.  The
          difference is that the rules for doing this would be in the
          HOL model, rather than prior to it.
*)



(* For the mss field cf TCPv2p865.  For the _val and _ecr fields cf TCPv2p867. *)


let 
(*:
segment well-formedness test (physical constraints imposed by format)
:*)
  sane_seg seg = LENGTH seg.data < (65536 - 40)


(*: @section [[netty_udp]] UDP UDP datagrams

UDP datagrams are very simple.  They are modelled as follows.

:*)

type 
  (*: UDP datagram type :*)
udpDatagram
  = <| udp_is1 : maybe ip;      (* source IP *)
       udp_is2 : maybe ip;      (* destination IP *)
       udp_ps1 : maybe port;     (* source port *)
       udp_ps2 : maybe port;     (* destination port *)
       udp_data : list byte
    |>


let 
(*:
message well-formedness test (physical constraints imposed by format)
:*)
  sane_udpdgm dgm = LENGTH dgm.udp_data < (65536-20-8)



(*: @section [[netty_icmp]] ALL ICMP datagrams

ICMP messages have \emph{type} and \emph{code} fields, both 8 bits wide.
The specification deals only with some of these types, as
characterised in the HOL type [[icmpType]] below.  For each type we
identify some or all of the codes that have conventional symbolic
representations, but to ensure the model can faithfully represent
arbitrary codes each code (HOL type) also has an [[OTHER]] constructor
carrying a byte.  The values carried are assumed not to overlap with
the symbolically-represented values.

In retrospect, there seems to be no reason not to have types
and codes simply particular byte constants.

:*)


type 
(*:
protocol type for use in ICMP messages
:*)
protocol = PROTO_TCP | PROTO_UDP


(* we believe our semantics does the right thing for all type-3 ICMPs (destination unreachable), hence we list them all here *)
type 
  icmp_unreach_code =
    NET
  | HOST
  | PROTOCOL
  | PORT
  | SRCFAIL
  | NEEDFRAG of maybe word16
  | NET_UNKNOWN
  | HOST_UNKNOWN
  | ISOLATED
  | NET_PROHIB
  | HOST_PROHIB
  | TOSNET
  | TOSHOST
  | FILTER_PROHIB
  | PREC_VIOLATION
  | PREC_CUTOFF
  | OTHER of byte * word32  (* really want this not to overlap *)
(* BSD drops ICMPs on the floor if their code is unrecognised, even if the type *is* recognised. *)

type 
  icmp_source_quench_code =
    QUENCH
  | SQ_OTHER of byte * word32 (*: writen [[OTHER]] :*)

(* FIXME val _ = overload_on("OTHER", ``SQ_OTHER``); *)

type 
  icmp_redirect_code =
    RD_NET                     (*: written [[NET]] :*)
  | RD_HOST                    (*: written [[HOST]] :*)
  | RD_TOSNET                  (*: written [[TOSNET]] :*)
  | RD_TOSHOST                 (*: written [[TOSHOST]] :*)
  | RD_OTHER of byte * word32  (*: written [[OTHER]] :*)

(* FIXME
val _ = overload_on("NET",     ``RD_NET``);
val _ = overload_on("HOST",    ``RD_HOST``);
val _ = overload_on("TOSNET",  ``RD_TOSNET``);
val _ = overload_on("TOSHOST", ``RD_TOSHOST``);
val _ = overload_on("OTHER",   ``RD_OTHER``);
*)

type 
  icmp_time_exceeded_code =
    INTRANS
  | REASS
  | TX_OTHER of byte * word32  (*: written [[OTHER]] :*)

(* FIXME val _ = overload_on("OTHER", ``TX_OTHER``); *)

type 
  icmp_paramprob_code =
    BADHDR
  | NEEDOPT
  | PP_OTHER of byte * word32  (*: written [[OTHER]] :*)

(* FIXME val _ = overload_on("OTHER", ``PP_OTHER``); *)


(* we list here only those ICMPs we handle.  The rest we explicitly do not specify in this semantics. *)
type
  icmpType =
    ICMP_UNREACH       of icmp_unreach_code
  | ICMP_SOURCE_QUENCH of icmp_source_quench_code
  | ICMP_REDIRECT      of icmp_redirect_code
  | ICMP_TIME_EXCEEDED of icmp_time_exceeded_code
  | ICMP_PARAMPROB     of icmp_paramprob_code
  (* FreeBSD 4.6-RELEASE also does: ICMP_ECHO, ICMP_TSTMP, ICMP_MASKREQ *)



type 
(*: ICMP datagram type :*)
  icmpDatagram
  = <| icmp_is1 : maybe ip;     (* this is the sender of this ICMP *)
       icmp_is2 : maybe ip;     (* this is the intended receiver of this ICMP *)

       (* we assume the enclosed IP always has at least 8 bytes of data, i.e., enough
          for all the fields below *)
       icmp_is3 : maybe ip;     (* source of enclosed IP datagram *)
       icmp_is4 : maybe ip;     (* destination of enclosed IP datagram *)
       icmp_ps3 : maybe port;    (* source port *)
       icmp_ps4 : maybe port;    (* destination port *)
       icmp_proto : protocol;     (* protocol *)
       icmp_seq : maybe tcp_seq_local ;  (* seq *)
       icmp_t   : icmpType
    |>


(* The above used to say: we assume the enclosed IP has proto=TCP; if
not, it's beyond the scope of this spec. But that now appears
outdated. *)


(*: @section [[netty_ip]] ALL IP messages

  An IP datagram is (for our purposes) either a TCP segment, an ICMP
  datagram, or a UDP datagram.  We use the type [[msg]] for IP
  datagrams.  IP datagrams may be checked for sanity, and may have
  their [[is1]] and [[is2]] fields inspected.

:*)

type 
(*: IP message type :*)
  msg = TCP of tcpSegment | ICMP of icmpDatagram | UDP of udpDatagram

let sane_msg x = (match x with
(*: message well-formedness test (physical constraints imposed by format) :*)
  | (TCP seg) -> sane_seg seg 
  | (ICMP dgm) -> true
  | (UDP dgm') -> sane_udpdgm dgm' 
end)


let msg_is1 x = (match x with 
  (*: source IP of a message, written [[x.is1]] :*)
  | (TCP seg)  -> seg.is1 
  | (ICMP dgm) -> dgm.icmp_is1 
  | (UDP dgm') -> dgm'.udp_is1
end)
 (*: @mergewithnext :*)
let msg_is2 x = (match x with 
  (*: destination IP of a message, written [[x.is2]] :*)
  | (TCP seg)  -> seg.is2 
  | (ICMP dgm) -> dgm.icmp_is2 
  | (UDP dgm') -> dgm'.udp_is2
end)

(* FIXME
val _ = Parse.add_record_field("is1",``msg_is1``);  (* overload x.is1 *)
val _ = Parse.add_record_field("is2",``msg_is2``);  (* overload x.is2 *)
*)

(* -------------------------------------------------- *)

(*
val _ = export_theory();
*)